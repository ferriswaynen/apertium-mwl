# Morphological Transducer for Mirandese

# Part of speech categories
# <n>      # Noun
# <v>      # Verb
# <adj>    # Adjective
# <num>    # Numeral
# <adv>    # Adverb
# <prn>    # Pronoun
# <gend>   # Gender

# <sent>   # Sentence-ending punctuation
# <guio>   # Hyphen
# <cm>     # Comma
# <apos>   # Apostrophe
# <quot>   # Quotation mark
# <lquot>  # Left quotation mark
# <rquot>  # Right quotation mark
# <lpar>   # Left parenthesis
# <rpar>   # Right parenthesis

# Number morphology
# <pl>     # Plural
# <sg>     # Singular

# Gender morphology
# <m>      # Masculine
# <f>      # Feminine
# <mf>     # Masculine and Feminine

# Verb morphology
# <tv>     # Transitive Verb
# <pres>   # Present
# <past>   # Past
# <fut>    # Future
# <fts>    # Future subjunctive
# <p1>     # First person
# <p2>     # Second person
# <p3>     # Third person
# <inf>    # Infinitive

# Pronoun morphology
# <pers>   # Personal
# <subj>   # Subjective
# <pos>    # Possessive
# <obj>    # Object

# Other symbols
# >        # Morpheme boundary

###############################################################################
###                       M O R P H O T A C T I C S                         ###
###############################################################################

PATTERNS
## Inflection patterns
(NounRoot RegNounInfl)[^[M,F]]
(AdjRoot RegAdjInfl)[^[M,F]]
AdvRoot RegAdvInfl
PrnRoot
# Make infinitives
(V-Stem Infin)[^[AR,ER,IR]]
(V-Stem V-SimplePresentIndicative)[^[AR,ER,IR]]
(V-Stem V-SimplePastIndicative)[^[AR,ER,IR]]
(V-Stem Infin V-SimpleFutureIndicative)[^[AR,ER,IR]]
(V-Stem Infin V-SimpleFutureSubjunctive)[^[AR,ER,IR]]
Preposition
Determiner
Verb
Conjunction
Contraction
Number
Punctuation


## Example inflection lexicon:
LEXICON RegNounInfl
<n><m><sg>:[M]
<n><f><sg>:[F]
<n><m><pl>:>{e}s[M]
<n><f><pl>:>{e}s[F]

LEXICON RegAdjInfl
<adj><m><sg>:[M]
<adj><f><sg>:[F]
<adj><m><pl>:>s[M]
<adj><f><pl>:>s[F]

LEXICON RegAdvInfl
<adv>:

LEXICON V-Stem
falar<v><tv>:fal[AR]
deber<v><tv>:deb[ER]
eisistir<v><tv>:eisist[IR]

LEXICON Infin
:a{accent}r[AR]
:e{accent}r[ER]
:i{accent}r[IR]


LEXICON V-SimplePresentIndicative
<pres><p1><sg>:o[AR]
<pres><p2><sg>:as[AR]
<pres><p3><sg>:a[AR]
<pres><p1><pl>:amos[AR]
<pres><p2><pl>:ais[AR]
<pres><p3><pl>:an[AR]
<pres><p1><sg>:o[ER]
<pres><p2><sg>:es[ER]
<pres><p3><sg>:e[ER]
<pres><p1><pl>:emos[ER]
<pres><p2><pl>:eis[ER]
<pres><p3><pl>:en[ER]
<pres><p1><sg>:o[IR]
<pres><p2><sg>:es[IR]
<pres><p3><sg>:e[IR]
<pres><p1><pl>:imos[IR]
<pres><p2><pl>:is[IR]
<pres><p3><pl>:en[IR]

LEXICON V-SimplePastIndicative
<past><p1><sg>:ei[AR]
<past><p2><sg>:este[AR]
<past><p3><sg>:ou[AR]
<past><p1><pl>:emos[AR]
<past><p2><pl>:estes[AR]
<past><p3><pl>:órun[AR]
<past><p1><sg>:i[ER]
<past><p2><sg>:iste[ER]
<past><p3><sg>:iu[ER]
<past><p1><pl>:imos[ER]
<past><p2><pl>:istes[ER]
<past><p3><pl>:írun[ER]
<past><p1><sg>:i[IR]
<past><p2><sg>:iste[IR]
<past><p3><sg>:iu[IR]
<past><p1><pl>:imos[IR]
<past><p2><pl>:istes[IR]
<past><p3><pl>:írun[IR]

LEXICON V-SimpleFutureIndicative
<fut><p1><sg>:ei[AR]
<fut><p2><sg>:ás[AR]
<fut><p3><sg>:á[AR]
<fut><p1><pl>:emos[AR]
<fut><p2><pl>:eis[AR]
<fut><p3><pl>:an[AR]
<fut><p1><sg>:ei[ER]
<fut><p2><sg>:ás[ER]
<fut><p3><sg>:á[ER]
<fut><p1><pl>:emos[ER]
<fut><p2><pl>:eis[ER]
<fut><p3><pl>:an[ER]
<fut><p1><sg>:ei[IR]
<fut><p2><sg>:ás[IR]
<fut><p3><sg>:á[IR]
<fut><p1><pl>:emos[IR]
<fut><p2><pl>:eis[IR]
<fut><p3><pl>:an[IR]

LEXICON V-SimpleFutureSubjunctive
<fts><p1><sg>:[AR]
<fts><p2><sg>:es[AR]
<fts><p3><sg>:[AR]
<fts><p1><pl>:mos[AR]
<fts><p2><pl>:des[AR]
<fts><p3><pl>:{subj}en[AR]
<fts><p1><sg>:[ER]
<fts><p2><sg>:es[ER]
<fts><p3><sg>:[ER]
<fts><p1><pl>:mos[ER]
<fts><p2><pl>:des[ER]
<fts><p3><pl>:{subj}en[ER]
<fts><p1><sg>:[IR]
<fts><p2><sg>:es[IR]
<fts><p3><sg>:[IR]
<fts><p1><pl>:mos[IR]
<fts><p2><pl>:des[IR]
<fts><p3><pl>:{subj}en[IR]

LEXICON Preposition
de<pr>:de
de>pr>:d' #Dir/LR
para<pr>:para
para<pr>:pa #Dir/LR
por<pr>:por
cun<pr>:cun
antes<pr>:antes
depuis<pr>:depuis
an<pr>:an
até<pr>:até
contra<pr>:contra
antre<pr>:antre

LEXICON Determiner
un<det><ind><m><sg>:un
un<det><ind><f><sg>:ua
l<det><def><m><sg>:l
l<det><def><m><pl>:ls
l<det><def><f><sg>:la
l<det><def><f><pl>:las

LEXICON Verb
tener<pres><p3><sg>:ten
deber<inf>:deber
bibir<inf>:bibir
ser<pres><p3><sg>:ye
chamar<pres><p3><sg>:chama
aplicar<pres><p3><sg>:aplica
nutrir<inf>:nutrir
ser<prs><p3><pl>:seian
digerir<inf>:digerir
cumprir<pres><p3><sg>:cumpre
porteger<inf>:porteger
cuntribuir<pres><p3><sg>:cuntribui
regular<ger>:regulando

LEXICON Conjunction
ó<cnjcoo>:ó
i<cnjcoo>:i
que<cnjsub>:que

LEXICON Contraction
por<pr>+l<det><def><f><pl>:pulas
de<pr>+un<det><ind><f><sg>:dua
a<pr>+l<det><def><m><pl>:als
de<pr>+esto<prn><dem><m><sg>:desso
an<pr>+l<det><def><f><pl>:nas

###############################################################################
###                          L E X I C O N                                  ###
###############################################################################

## Grammatical gender:hfst-expand -c0 xyz.automorf.hfst
LEXICON NounRoot

 # A noun
home:home[M]
fator:fator[M]
boda:boda[F]
arble:arble[F]
nome:nome[M]
gato:gato[M]
cumputador:cumputador[M]
papel:papel[M]
beç:be{Ç}[M]
paíç:paí{Ç}[M]
nino:nino[M]
deber:deber[M]
lhéngua:lhéngua[F]
lheite:lheite[M]
lheite:leite[M] #Dir/LR
secreçon:secreçon[F]
quelor:quelor[M]
glándula:glándula[F]
fémea:fémea[F]
mamífero:mamífero[M]
lhíquido:lhíquido[M]
célula:célula[F]
mama:mama[F]
die:die[F]
parto:parto[M]
colostro:colostro[M]
spece:spece[F]
mamilo:mamilo[M]
suco:suco[M]
planta:planta[F]
fruito:fruito[M]
coco:coco[M]
soja:soja[F]
arroç:arroç[M]
almendra:almendra[F]
todo:todo[M]
defeniçon:defeniçon[F]
termo:termo[M]
nuoç:nuoç[M]
funçon:funçon[F]
filhote:filhote[M]
quemido:quemido[M]
trato:trato[M]
crio:cria[F]
antígeno:antígeno[M]
toxina:toxina[F]
anflamaçon:anflamaçon[F]
salude:salude[F]
porcesso:porcesso[M]
oubtençon:oubtençon[F]
einergie:einergie[F]
base:base[F]
lhaticínio:lhaticínio[M]
manteiga:manteiga[F]
queiso:queiso[M]
eiagurte:eiagurte[M]
outro:outro[M]
uso:uso[M]
deribado:deribado[M]
andústria:andústria[F]
perduto:perduto[M]
pó:pó[M]
soro:soro[M]
caseína:caseína[F]
lhatose:lhatose[F]

LEXICON AdjRoot

# An adjective
buono:buono[M]
buono:buona[F]
negro:negro[M]
negro:negra[F]
rápido:rápido[M]
rápido:rápida[F]
chinés:chinesa[F]
cunciso:cuncisa[F]
nutritibo:nutritiba[F]
sbranquiçado:sbranquiçada[F]
oupaco:oupaca[F]
porduzido:porduzida[F]
mamário:mamária[F]
mama:mama[MF]
secretor:secretora[F]
lháteo:lhátea[F]
dous:dues[F]
cada:cada[MF]
cierto:cierta[F]
científico:científica[F]
percipal:percipal[F]
capaç:capaç[M]
outro:outro[M]
gastroantestinal:gastroantestinal[M]
metabólico:metabólica[F]
numeroso:numeroso[M]
frequente:frequente[M]
alimentício:alimentícia[F]
químico:química[F]
farmacéutico:farmacéutica[F]
cundensado:cundensado[M]

LEXICON AdvRoot

# An adverb
frequentemente:frequentemente
manhana:manhana
lhougo:lhougo
lhougo:lougo #Dir/LR
plenamente:plenamente
mui:mui
tamien:tamien
assi:assi
assi\i\todo:assi\i\todo
nun:nun
alhá:alhá
cumo:cumo

LEXICON PrnRoot

# A pronoun
you<prn><pers><p1><sg><subj>:you
tu<prn><pers><p2><sg><subj>:tu
el<prn><pers><p3><m><sg><subj>:el
eilha<prn><pers><p3><f><sg><subj>:eilha
nós<prn><pers><p1><pl><subj>:nós
bós<prn><pers><p2><pl><subj>:bós
eilhes<prn><pers><p3><m><pl><subj>:eilhes
eilhas<prn><pers><p3><f><pl><subj>:eilhas
you<prn><pers><p1><sg><pos>:miu
tu<prn><pers><p2><sg><pos>:tou
el<prn><pers><p3><m><sg><pos>:sou
eilha<prn><pers><p3><f><sg><pos>:sou
nós<prn><pers><p1><pl><pos>:nuosso
bós<prn><pers><p2><pl><pos>:buosso
eilhes<prn><pers><p3><m><pl><pos>:sou
eilhas<prn><pers><p3><f><pl><pos>:sou
you<prn><pers><p1><sg><obj>:me
tu<prn><pers><p2><sg><obj>:te
el<prn><pers><p3><m><sg><obj>:lo
el<prn><pers><p3><m><sg><obj>:l #Dir/LR
eilha<prn><pers><p3><f><sg><obj>:la
nós<prn><pers><p1><pl><obj>:nos
bós<prn><pers><p2><pl><obj>:bos
eilhes<prn><pers><p3><m><pl><obj>:los
eilhas<prn><pers><p3><f><pl><obj>:las
el<prn><pers><p3><m><sg><ref>:se

PATTERN Number
Numeral+ [<num>:]

LEXICON Numeral
0
1
2
3
4
5
6
7
8
9


LEXICON Punctuation

.<sent>:.
..<sent>:..
...<sent>:...
;<sent>:;
\:<sent>:\:
!<sent>:!
-<guio>:-
—<guio>:—
,<cm>:,
?<sent>:?
'<apos>:'
"<quot>:"
«<lquot>:«
»<rquot>:»
”<rquot>:”
“<lquot>:“
(<lpar>:(
\]<rpar>:\]
\[<lpar>:\[
)<rpar>:)
\\<sent>:\\
\/<sent>:\/


# Resources:
# https://github.com/apertium/lexd/blob/master/Usage.md
# https://wiki.apertium.org/wiki/List_of_symbols
# https://wiki.apertium.org/wiki/Starting_a_new_language_with_HFST#Lexicon
# https://wiki.apertium.org/wiki/Lttoolbox_and_lexc
# https://wiki.apertium.org/wiki/Apertium-specific_conventions_for_lexc
# https://kitwiki.csc.fi/twiki/bin/view/KitWiki/HfstHome
# https://github.com/hfst/
# https://wiki.apertium.org/wiki/Hfst

